Program → ε
        | Program FunctionDecl

FunctionDecl → 'fn' IDENTIFIER '(' ParamList ')' '->' Type '{' StmtList '}'

ParamList → ε
          | ParamListNonEmpty

ParamListNonEmpty → Param
                  | ParamListNonEmpty ',' Param

Param → Type IDENTIFIER

Type → BaseType
     | BaseType '[' INTEGER_LITERAL ']'
     | BaseType '*'

BaseType → 'int'
         | 'float'
         | 'char'

StmtList → ε
         | StmtList Stmt

Stmt → SimpleStmt ';'
     | '{' StmtList '}'
     | IfStmt
     | WhileStmt
     | ForStmt

SimpleStmt → VarDeclExpr
           | AssignExpr
           | IDENTIFIER '(' ExprList ')'
           | 'ret' ExprOpt

VarDeclExpr → Type IDENTIFIER InitOpt

InitOpt → ε
        | '=' AssignValue

AssignValue → Expr
            | '{' ExprList '}'

AssignExpr → AssignTarget AssignOp Expr

AssignTarget → IDENTIFIER
             | IDENTIFIER '[' Expr ']'
             | '*' IDENTIFIER

AssignOp → '='
         | '+='
         | '-='
         | '/='
         | '*='
         | '&='
         | '|='
         | '^='
         | '~='

IfStmt → 'if' '(' ConditionOp ')' Body
       | 'if' '(' ConditionOp ')' Body 'else' Body

WhileStmt → 'while' '(' ConditionOp ')' Body

ConditionOp → Expr
            | AssignExpr

ForStmt → 'for' '(' ForInit ';' ExprOpt ';' ForUpdate ')' Body

ForInit → ε
        | VarDeclExpr
        | AssignExpr

ExprOpt → ε
        | Expr

ForUpdate → ε
          | AssignExpr
          | IncrementExpr

Body → '{' StmtList '}'
     | SimpleStmt ';'
     | IfStmt
     | WhileStmt
     | ForStmt

ExprList → ε
         | ExprListNonEmpty

ExprListNonEmpty → Expr
                 | ExprListNonEmpty ',' Expr

Expr → LogicalExpr
     | AddressExpr

LogicalExpr → RelationalExpr
            | LogicalExpr '||' RelationalExpr
            | LogicalExpr '&&' RelationalExpr

RelationalExpr → AddExpr
               | RelationalExpr '==' AddExpr
               | RelationalExpr '!=' AddExpr
               | RelationalExpr '<' AddExpr
               | RelationalExpr '>' AddExpr
               | RelationalExpr '<=' AddExpr
               | RelationalExpr '>=' AddExpr

AddExpr → MulExpr
        | AddExpr '+' MulExpr
        | AddExpr '-' MulExpr
        | AddExpr '|' MulExpr
        | AddExpr '^' MulExpr

MulExpr → UnaryExpr
        | MulExpr '*' UnaryExpr
        | MulExpr '/' UnaryExpr
        | MulExpr '&' UnaryExpr

UnaryExpr → PrimaryExpr
          | '-' UnaryExpr
          | '!' UnaryExpr

IncrementExpr → IDENTIFIER '++'
              | IDENTIFIER '--'
              | '++' IDENTIFIER
              | '--' IDENTIFIER

AddressExpr → '&' IDENTIFIER

DereferenceExpr → '*' IDENTIFIER

PrimaryExpr → IDENTIFIER
            | IDENTIFIER '[' Expr ']'
            | INTEGER_LITERAL
            | FLOAT_LITERAL
            | CHAR_LITERAL
            | '(' Expr ')'
            | IDENTIFIER '(' ExprList ')'
            | DereferenceExpr
            | IncrementExpr
