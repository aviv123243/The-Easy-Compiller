<Program> ::= ε
            | <Program> <FunctionDecl>

<FunctionDecl> ::= KEYWORD_FN IDENTIFIER OPEN_PAREN <ParamList> CLOSED_PAREN RIGHT_ARROW <Type> OPEN_CURLY <StmtList> CLOSED_CURLY

<ParamList> ::= ε
             | <ParamListNonEmpty>

<ParamListNonEmpty> ::= <Param>
                     | <ParamListNonEmpty> COMMA <Param>

<Param> ::= <Type> IDENTIFIER

<Type> ::= <BaseType>
        | <BaseType> OPEN_BRACKET INTEGER_LITERAL CLOSED_BRACKET
        | <BaseType> STAR

<BaseType> ::= KEYWORD_INT
            | KEYWORD_FLOAT
            | KEYWORD_CHAR

<StmtList> ::= ε
            | <StmtList> <Stmt>

<Stmt> ::= <SimpleStmt> SEMICOLON
        | OPEN_CURLY <StmtList> CLOSED_CURLY
        | <IfStmt>
        | <WhileStmt>
        | <ForStmt>

<SimpleStmt> ::= <VarDeclExpr>
              | <AssignExpr>
              | IDENTIFIER OPEN_PAREN <ExprList> CLOSED_PAREN
              | KEYWORD_RET <ExprOpt>

<VarDeclExpr> ::= <Type> IDENTIFIER <InitOpt>

<InitOpt> ::= ε
           | EQUALS <AssignValue>

<AssignValue> ::= <Expr>
               | OPEN_CURLY <ExprList> CLOSED_CURLY

<AssignExpr> ::= <AssignTarget> <AssignOp> <Expr>

<AssignTarget> ::= IDENTIFIER
                | IDENTIFIER OPEN_BRACKET <Expr> CLOSED_BRACKET
                | STAR IDENTIFIER

<AssignOp> ::= EQUALS
            | PLUS_EQUALS
            | MINUS_EQUALS
            | SLASH_EQUALS
            | STAR_EQUALS
            | AMPERSAND_EQUALS
            | PIPE_EQUALS
            | CARET_EQUALS
            | TILDE_EQUALS

<IfStmt> ::= KEYWORD_IF OPEN_PAREN <ConditionOp> CLOSED_PAREN <Body>
          | KEYWORD_IF OPEN_PAREN <ConditionOp> CLOSED_PAREN <Body> KEYWORD_ELSE <Body>

<WhileStmt> ::= KEYWORD_WHILE OPEN_PAREN <ConditionOp> CLOSED_PAREN <Body>

<ConditionOp> ::= <Expr>
               | <AssignExpr>

<ForStmt> ::= KEYWORD_FOR OPEN_PAREN <ForInit> SEMICOLON <ExprOpt> SEMICOLON <ForUpdate> CLOSED_PAREN <Body>

<ForInit> ::= ε
           | <VarDeclExpr>
           | <AssignExpr>

<ExprOpt> ::= ε
           | <Expr>

<ForUpdate> ::= ε
             | <AssignExpr>
             | <IncrementExpr>

<Body> ::= OPEN_CURLY <StmtList> CLOSED_CURLY
        | <SimpleStmt> SEMICOLON
        | <IfStmt>
        | <WhileStmt>
        | <ForStmt>

<ExprList> ::= ε
            | <ExprListNonEmpty>

<ExprListNonEmpty> ::= <Expr>
                    | <ExprListNonEmpty> COMMA <Expr>

<Expr> ::= <LogicalExpr>
        | <AddressExpr>

<LogicalExpr> ::= <RelationalExpr>
               | <LogicalExpr> PIPE_PIPE <RelationalExpr>
               | <LogicalExpr> AMPERSAND_AMPERSAND <RelationalExpr>

<RelationalExpr> ::= <AddExpr>
                  | <RelationalExpr> EQUALS_EQUALS <AddExpr>
                  | <RelationalExpr> BANG_EQUALS <AddExpr>
                  | <RelationalExpr> LESS_THAN <AddExpr>
                  | <RelationalExpr> GREATER_THAN <AddExpr>
                  | <RelationalExpr> LESS_THAN_EQUALS <AddExpr>
                  | <RelationalExpr> GREATER_THAN_EQUALS <AddExpr>

<AddExpr> ::= <MulExpr>
           | <AddExpr> PLUS <MulExpr>
           | <AddExpr> MINUS <MulExpr>
           | <AddExpr> PIPE <MulExpr>
           | <AddExpr> CARET <MulExpr>

<MulExpr> ::= <UnaryExpr>
           | <MulExpr> STAR <UnaryExpr>
           | <MulExpr> SLASH <UnaryExpr>
           | <MulExpr> AMPERSAND <UnaryExpr>

<UnaryExpr> ::= <PrimaryExpr>
             | MINUS <UnaryExpr>
             | BANG <UnaryExpr>

<IncrementExpr> ::= IDENTIFIER PLUS_PLUS
                 | IDENTIFIER MINUS_MINUS
                 | PLUS_PLUS IDENTIFIER
                 | MINUS_MINUS IDENTIFIER

<AddressExpr> ::= AMPERSAND IDENTIFIER

<DereferenceExpr> ::= STAR IDENTIFIER

<PrimaryExpr> ::= IDENTIFIER
               | IDENTIFIER OPEN_BRACKET <Expr> CLOSED_BRACKET
               | INTEGER_LITERAL
               | FLOAT_LITERAL
               | CHAR_LITERAL
               | OPEN_PAREN <Expr> CLOSED_PAREN
               | IDENTIFIER OPEN_PAREN <ExprList> CLOSED_PAREN
               | <DereferenceExpr>
               | <IncrementExpr>
